plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
	id 'jacoco'
	id 'org.sonarqube' version '6.0.1.5171'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Individual preparation for the Advanced Programming course at the Faculty of Computer Science Universitas Indonesia'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Checkstyle: JANGAN tambahin guava manual di sini
	checkstyle 'com.puppycrawl.tools:checkstyle:10.12.5'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}

sonar {
	properties {
		property "sonar.projectKey", "advprog-2026-A3-project_individual-preparation"
		property "sonar.organization", "advprog-a3"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

checkstyle {
	toolVersion = "10.12.5"
	configFile = file("config/checkstyle/checkstyle.xml")
}

// FIX konflik guava vs google-collections, khusus konfigurasi Checkstyle saja
configurations.configureEach { cfg ->
	if (cfg.name.toLowerCase().startsWith("checkstyle")) {
		cfg.exclude group: "com.google.collections", module: "google-collections"
		cfg.resolutionStrategy.force "com.google.guava:guava:33.3.1-jre"
	}
}
